= Using APOC for Import
:slug: 03-import-40-using-apoc-import
:doctype: book
:toc: left
:toclevels: 4
:imagesdir: ../images
:page-slug: {slug}
:page-layout: training
:page-quiz:
:page-module-duration-minutes: 30

== About this module

You have just learned how import data into the graph using Cypher's `LOAD CSV` clause.
This is one of the easiest ways to import data, but it has its limitations.
Next, you will learn how you can use some of the APOC procedures to help you import data into the graph.

At the end of this module, you should be able to:

[square]
* Ensure the APOC library is available.
* Clear a graph of constraints, indexes, nodes, and relationships using APOC.
* Perform conditional processing with APOC during import.
* Use APOC for importing a CSV file.
* Use APOC for importing a JSON file.

[IMPORTANT]
Because the code examples in this lesson modify the database, it is recommended that you [.underline]#do not# execute them against your database as you will be doing so in the hands-on exercises.

== Installing APOC for use with your graph(s)


If you are using a Neo4j Sandbox or Neo4j Aura, your database already has access to the APOC library.
If you are using Neo4j Desktop, you must add the APOC library to your database server you are using.

Here are the steps for adding the APOC library to your database server in Neo4j Desktop:

. Open the details for DBMS you are working with, by clicking the DBMS.
. Select the *Plugins* tab.
. Select APOC to install.
. The DBMS should automatically restart.

=== Confirming APOC is available

You can confirm in Neo4j Browser that you have APOC available by executing this code:

[source,Cypher,role=noplay]
----
CALL dbms.procedures()
YIELD name WHERE name STARTS WITH "apoc"
RETURN name
----

image::APOCInstalled.png[APOCInstalled,width=600,align=center]

== Using APOC to clear the graph


When you are developing code to import data, you may have several attempts to perform the import correctly.
Rather than creating a new database for each attempt, you can completely clear the database of all constraints, indexes, nodes, and relationships.


Here is one way that you can clear the database:

[source,Cypher,role=nocopy noplay]
----
// Delete all constraints and indexes
CALL apoc.schema.assert({},{},true);
// Delete all nodes and relationships
CALL apoc.periodic.iterate(
  'MATCH (n) RETURN n',
  'DETACH DELETE n',
  { batchSize:500 }
)
----

image::ClearDatabase.png[ClearDatabase,width=900,align=center]

[NOTE]
In Neo4j 4.x, another way that you can clear the graph in Neo4j Browser is to use the _system_ database and then type `CREATE OR REPLACE DATABASE <database-name>`.

== Using APOC during import

One benefit of using APOC for loading data into the graph is that it can sometimes be faster than `LOAD CSV`.
In addition, APOC has some procedures that are helpful during the load, one of which is to control conditional processing.
And as you have already learned, with APOC, you can load large datasets that will fail if using `LOAD CSV` or even `:auto USING PERIODIC COMMIT LOAD CSV`.

Just as you inspect the data, determine if data needs to be transformed, and create uniqueness constraints before the import with `LOAD CSV`,
you must do the same when using APOC for the import.

Here is an example of the various types of loading procedures you can use in APOC:

image::APOCLOADProcedures.png[APOCLOADProcedures,width=1000,align=center]

== Using APOC for conditional processing

In the previous lesson, we used `LOAD CSV` to load Movie and Person data into the graph and then use the additional CSV files to create the relationships between the nodes.
Those files represented normalized data where each file basically represents a relational table.

If you want to load denormalized data from a CSV file, you face a couple of challenges.
Just as a reminder, here is a snippet of a denormalized CSV file:

image::DenormalizedData.png[DenormalizedData,width=900,align=center]

[.statement]
To load this data into the graph you could:

[source,example,role=nocopy noplay]
--
// Make a pass through the file to load the Movie nodes.

// Make a pass through the file to load the Person nodes.

// Make a pass through the file to create relationships based upon the personType field.
--

== Better alternative for conditional processing with APOC

If the CSV files are large, making multiple passes might not be ideal if you have load time constraints.
A better option might be to:

. Make a pass through the file to load the _Movie_ nodes, collect the person data and then add the _Person_ nodes from the collection.
. Use the person data to create relationships *based upon* the _personType_ field.

=== Import nodes, then create relationships

Assuming that we will use the second option for importing the data and we have created the uniqueness constraints as before, here is the Cypher code to create the _Person_ and _Movie_ nodes:

[source,Cypher,role=nocopy noplay]
----
CREATE CONSTRAINT UniqueMovieIdConstraint ON (m:Movie) ASSERT m.id IS UNIQUE;
CREATE CONSTRAINT UniquePersonIdConstraint ON (p:Person) ASSERT p.id IS UNIQUE;

// import the people and movie data (partial; no relationships)
LOAD CSV WITH HEADERS FROM
     'https://data.neo4j.com/v4.0-intro-neo4j/movies2.csv' AS row
WITH row.movieId as movieId, row.title AS title, row.genres AS genres,
     toInteger(row.releaseYear) AS releaseYear, toFloat(row.avgVote) AS avgVote,
     collect({id: row.personId, name:row.name, born: toInteger(row.birthYear),
        died: toInteger(row.deathYear),personType: row.personType,
        roles: split(coalesce(row.characters,""),':')}) AS personData
MERGE (m:Movie {id:movieId})
   ON CREATE SET m.title=title, m.avgVote=avgVote,
      m.releaseYear=releaseYear, m.genres=split(genres,":")
WITH *
UNWIND personData AS person
MERGE (p:Person {id: person.id})
   ON CREATE SET p.name = person.name, p.born = person.born, p.died = person.died
----

This code reads the data from a _row_ and creates the _personData_ collection that holds the data for a person.
It creates the _Movie_ nodes based upon the _row_ data.
With the `WITH *` clause, all variables are carried forward in the query.
Then the _personData_ collection is unwound so that each element in a row can be used to create the _Person_ nodes.
Everything is in the graph, except for the relationships.

image::APOC-noRelationships.png[APOC-noRelationships,width=600,align=center]

This is not quite what we want because we have not created the relationships.
That is, the type of relationship created depends on the value of the _personType_ field in each row of the CSV file.
This is where APOC can help  you.
APOC has a procedure that will allow you to perform conditional execution, based upon a value.

=== Example: Using APOC for conditional processing

Here is the complete code that utilizes the `apoc.do.when()` procedure, assuming that we have cleared the data from the graph first, but the constraints are still defined.:

[source,Cypher,role=nocopy noplay]
----
LOAD CSV WITH HEADERS FROM
     'https://data.neo4j.com/v4.0-intro-neo4j/movies2.csv' AS row
WITH row.movieId as movieId, row.title AS title, row.genres AS genres,
     toInteger(row.releaseYear) AS releaseYear, toFloat(row.avgVote) AS avgVote,
     collect({id: row.personId, name:row.name, born: toInteger(row.birthYear),
            died: toInteger(row.deathYear),personType: row.personType,
            roles: split(coalesce(row.characters,""),':')}) AS people
MERGE (m:Movie {id:movieId})
   ON CREATE SET m.title=title, m.avgVote=avgVote,
      m.releaseYear=releaseYear, m.genres=split(genres,":")
WITH *
UNWIND people AS person
MERGE (p:Person {id: person.id})
   ON CREATE SET p.name = person.name, p.born = person.born, p.died = person.died
// continue processing and use the personType to create the relationships
WITH  m, person, p
CALL apoc.do.when(person.personType = 'ACTOR',
     "MERGE (p)-[:ACTED_IN {roles: person.roles}]->(m)
                ON CREATE SET p:Actor",
     "MERGE (p)-[:DIRECTED]->(m)
         ON CREATE SET p:Director",
     {m:m, p:p, person:person}) YIELD value
RETURN count(*)  // cannot end query with this type of APOC call
----

After the _Movie_ and _Person_ nodes are created, we use the reference to them to create the relationships between them.
The first argument to `apoc.do.when()` is the data that is tested.
The second argument is the Cypher code to execute if the test returns true.
The third argument is the Cypher code to execute if the test returns false.
The last argument is the object that describes the mapping of variables both outside of the call and inside the call.
For simplicity, we specify the same values.
Certain `apoc` calls cannot end a Cypher query so we place a `RETURN count(*)` at the end.

Here is the result:

image::DoWhenAPOC.png[DoWhenAPOC,width=600,align=center]

== Using APOC to import from CSV

If you cannot load the CSV file with `LOAD CSV` or `:auto USING PERIODIC COMMIT LOAD CSV`, another option is to use APOC for the import.
Previously, you learned how to clear the data from the graph using `apoc.periodic.iterate()`.
You use this procedure to load large datasets.

Here is an example with an empty database, but with the constraints defined for the _Person.id_ and _Movie.id_ properties:

[source,Cypher,role=nocopy noplay]
----
CALL apoc.periodic.iterate(
"CALL apoc.load.csv('https://data.neo4j.com/v4.0-intro-neo4j/movies2.csv' )
 YIELD map AS row RETURN row",
 "WITH row.movieId as movieId, row.title AS title, row.genres AS genres,
      toInteger(row.releaseYear) AS releaseYear, toFloat(row.avgVote) AS avgVote,
      collect({id: row.personId, name:row.name, born: toInteger(row.birthYear),
      died: toInteger(row.deathYear),personType: row.personType,
      roles: split(coalesce(row.characters,''),':')}) AS people
 MERGE (m:Movie {id:movieId})
    ON CREATE SET m.title=title, m.avgVote=avgVote,
       m.releaseYear=releaseYear, m.genres=split(genres,':')
 WITH *
 UNWIND people AS person
 MERGE (p:Person {id: person.id})
   ON CREATE SET p.name = person.name, p.born = person.born, p.died = person.died
 WITH  m, person, p
 CALL apoc.do.when(person.personType = 'ACTOR',
      'MERGE (p)-[:ACTED_IN {roles: person.roles}]->(m)
                 ON CREATE SET p:Actor',
      'MERGE (p)-[:DIRECTED]->(m)
          ON CREATE SET p:Director',
      {m:m, p:p, person:person}) YIELD value AS value
       RETURN count(*)  ",
{batchSize: 500}
)
----

The first argument to `apoc.periodic.iterate()` is the call to `apoc.load.csv()` where we provide the file name and it returns a _row_.
The second argument is the same Cypher code you saw earlier.
The only thing that is different is that you must ensure that the code is in double quotes and the Cypher code does not use double-quotes (or visa versa).
The final argument is the size of the batch, 500.

=== Result of APOC load

Here is the result:

image::APOCPeriodicIterate.png[APOCPeriodicIterate,width=1300,align=center]

== Using APOC to load JSON

JSON is another data format you might need to import into a graph.
There are many data sources out there that can provide data in JSON format.
For this course, we will use the StackOverflow data.
Your first step should be to become familiar with the data that you want to load into the graph.

In this example we call `apoc.load.json` to return 10 questions from StackOverflow so we can view them:

[source,Cypher,role=noplay]
----
WITH "https://api.stackexchange.com/2.2/search?page=1&pagesize=5&order=asc&sort=creation&tagged=neo4j&site=stackoverflow&filter=!5-i6Zw8Y)4W7vpy91PMYsKM-k9yzEsSC1_Uxlf" AS uri
CALL apoc.load.json(uri)
YIELD value AS data
UNWIND data.items as q
RETURN q
----

We specify _pagesize_, 5 in the URI. This retrieves 5 questions.
We then `UNWIND` the data and return each question, _q_.

Here is the result:

image::ExamineJSONData.png[ExamineJSONData,width=600,align=center]

=== Viewing keys in the JSON data

To help you understand the types of data available for each question, you can return the keys for each row:

[source,Cypher,role=noplay]
----
WITH "https://api.stackexchange.com/2.2/search?page=1&pagesize=5&order=asc&sort=creation&tagged=neo4j&site=stackoverflow&filter=!5-i6Zw8Y)4W7vpy91PMYsKM-k9yzEsSC1_Uxlf" AS uri
CALL apoc.load.json(uri)
YIELD value AS data
UNWIND data.items as q
RETURN keys(q)
----

We specify _pagesize_, 5 in the URI. This retrieves 5 questions.
We then `UNWIND` the data and return each question, _q_.

Here is the result:

image::ExamineJSONDataKeys.png[ExamineJSONDataKeys,width=800,align=center]

=== Viewing data that will be loaded

Next, you must determine what data from the JSON file  you will use to create the graph.

Here we have made a selection for the data we want to create in the graph and we write the code to return it:

[source,Cypher,role=noplay]
----
WITH "https://api.stackexchange.com/2.2/search?page=1&pagesize=5&order=asc&sort=creation&tagged=neo4j&site=stackoverflow&filter=!5-i6Zw8Y)4W7vpy91PMYsKM-k9yzEsSC1_Uxlf" AS uri
CALL apoc.load.json(uri)
YIELD value AS data
UNWIND data.items as q
RETURN q.question_id, q.title, q.tags, q.is_answered, q.owner.display_name
----

Here is the result:

image::SOQuestionData.png[SOQuestionData,width=1300,align=center]

=== Creating the graph from JSON

We will use all values, except _owner.display_name_ to create a _Question_ node. We will use _owner.display_name_ to create the _User_ nodes.
Here is the code to create the graph:

[source,Cypher,role=nocopy noplay]
----
WITH "https://api.stackexchange.com/2.2/search?page=1&pagesize=5&order=asc&sort=creation&tagged=neo4j&site=stackoverflow&filter=!5-i6Zw8Y)4W7vpy91PMYsKM-k9yzEsSC1_Uxlf" AS uri
CALL apoc.load.json(uri)
YIELD value AS data
UNWIND data.items as q
MERGE (question:Question {id: q.question_id})
  ON CREATE SET  question.title = q.title,
                 question.tags = q.tags,
                 question.is_answered = q.is_answered
MERGE (user:User {name: q.owner.display_name})
MERGE (user)-[:POSTED]->(question)
----

Here is the result of querying the nodes in the graph after the load:

image::SOLoadedGraph.png[SOLoadedGraph,width=500,align=center]

If you were to load thousands or more questions, you should ensure that you have created uniqueness constraints on _Question.question_id_ and _User.name_ before you attempt the load.

[.student-exercise]
== Exercise: Using APOC for importing data

In the query edit pane of Neo4j Browser, execute the browser command:

kbd:[:play 4.0-intro-neo4j-exercises]

and follow the instructions for Exercise 17.

[NOTE]
This exercise has 6 steps.
Estimated time to complete: 15 minutes.

[.quiz]
== Check your understanding

=== Question 1

[.statement]
What APOC procedure can you use to batch transactions when a lot of data needs to be processed?

[.statement]
Select the correct answer.

[%interactive.answers]
- [ ] `apoc.batch()`
- [ ] `apoc.transaction.batch()`
- [ ] `apoc.iterate()`
- [x] `apoc.periodic.iterate()`

=== Question 2

[.statement]
The procedure `apoc.do.when()` is used for:

[.statement]
Select the correct answers.

[%interactive.answers]
- [ ] Scheduling when a load should occur.
- [x] Executing Cypher code when a condition is true and alternate Cypher code when the condition is false.
- [ ] An alternative to the `MERGE` clause.
- [x] Understanding how many operations occurred during the execution.

=== Question 3

[.statement]
What does `CALL apoc.schema.assert({},{},true)` do?

[.statement]
Select the correct answers.

[%interactive.answers]
- [x] Drops all constraints in the graph.
- [x] Drops all indexes in the graph.
- [ ] Removes all relationships in the graph.
- [ ] Removes all nodes in the graph.

[.summary]
== Summary

You should now be able to:

[square]
* Ensure the APOC library is available.
* Clear a graph of constraints, indexes, nodes, and relationships using APOC.
* Perform conditional processing with APOC during import.
* Use APOC for importing a CSV file.
* Use APOC for importing a JSON file.

