= Exercise 9
== Exercise 9: Merging Data in the Graph (Preparations)

Make sure you have the Movie database, which contains 172 nodes and 253 relationships.

This is what you should see when you click the database icon:

image::AfterExercise8.png[After exercise 8,300, role=left]

If your database does not have this number of nodes and relationships, you can use the script below to reset it:

[source, cypher]
----
include::scripts/AfterExercise8.cypher[]
----

== Exercise 9: Merging Data in the Graph (Overview)

You have gained experience creating, modifying, and deleting nodes and relationships.

In this exercise you will add and modify nodes and relationships in the graph by merging data.
In some parts of the exercise, you will be creating duplicate nodes and relationships.
You will see how in your real application, you should take care not to create duplicate nodes and relationships in your graphs.

* *Exercise 9.1*: Use `MERGE` to create a _Movie_ node.
* *Exercise 9.2*: Use `MERGE` to update a node.
* *Exercise 9.3*: Use `MERGE` to create a _Production_ node.
* *Exercise 9.4*: Find all labels for nodes with a property value.
* *Exercise 9.5*: Use `MERGE` to update a _Production_ node.
* *Exercise 9.6*: Use `MERGE` to add a label to a node.
* *Exercise 9.7*: Use `MERGE` to create two nodes and a single relationship.
* *Exercise 9.8*: Use the same `MERGE` statement to attempt to create two nodes and a single relationship.
* *Exercise 9.9*: Find the correct _Person_ node to delete.
* *Exercise 9.10*: Delete this _Person_ node, along with its relationships.
* *Exercise 9.11*: Find the correct _Forrest Gump_ node to delete.
* *Exercise 9.12*: Delete the _Forrest Gump_ node.
* *Exercise 9.13*: Use `MERGE` to create the _DIRECTED_ relationship.
* *Exercise 9.14*: Use `MERGE` to create the _ACTED_IN_ relationship.
* *Exercise 9.15*: Modify the _role_ relationship property.

Go to the next page to start this exercise.

== Exercise 9.1: Use `MERGE` to create a _Movie_ node (Instructions)

In this Part of the exercise, you will again create the movie, _Forrest Gump_, update the node, and then merge the data in the graph to create another node with a different label.

*Use `MERGE` to create (`ON CREATE`) a node of type _Movie_ with the _title_ property, _Forrest Gump_. If created,  set the _released_ property to  _1994_.*

== Exercise 9.1: Use `MERGE` to create a _Movie_ node (Solution)

*Use `MERGE` to create (`ON CREATE`) a node of type _Movie_ with the _title_ property, _Forrest Gump_. If created,  set the _released_ property to  _1994_.*

[source, cypher]
----
MERGE (m:Movie {title: 'Forrest Gump'})
ON CREATE SET m.released = 1994
RETURN m
----

The result returned should be:

[.thumb]
image::CreateForrestGump2.png[CreateForrestGump2,width=700]

== Exercise 9.2: Use `MERGE` to update a node (Instructions)

*Use `MERGE` to update (`ON MATCH`) a node of type _Movie_ with the _title_ property, _Forrest Gump_. If found, set the tagline property to "Life is like a box of chocolates...you never know what you're gonna get.".*

== Exercise 9.2: Use `MERGE` to update a node (Solution)

*Use `MERGE` to update (`ON MATCH`) a node of type _Movie_ with the _title_ property, _Forrest Gump_. If found, set the tagline property to "Life is like a box of chocolates...you never know what you're gonna get.".*

[source, cypher]
----
MERGE (m:Movie {title: 'Forrest Gump'})
ON MATCH SET m.tagline = "Life is like a box of chocolates...you never know what you're gonna get."
RETURN m
----

The result returned should be:

[.thumb]
image::MergeForrestGump.png[MergeForrestGump,width=700]

== Exercise 9.3: Use `MERGE` to create a _Production_ node (Instructions)

*Use `MERGE` to create (`ON CREATE`) a node of type _Production_ with the _title_ property, _Forrest Gump_. If created, set the property _year_ to the value _1994_.*

== Exercise 9.3: Use `MERGE` to create a _Production_ node (Solution)

*Use `MERGE` to create (`ON CREATE`) a node of type _Production_ with the _title_ property, _Forrest Gump_. If created, set the property _year_ to the value _1994_.*

[source, cypher]
----
MERGE (p:Production {title: 'Forrest Gump'})
ON CREATE SET p.year = 1994
RETURN p
----

The result returned should be:

[.thumb]
image::MergeProduction.png[MergeProduction,width=700]

== Exercise 9.4: Find all labels for nodes with a property value (Instructions)

*Query the graph to find labels for nodes with the title property, _Forrest Gump_.*

== Exercise 9.4: Find all labels for nodes with a property value (Solution)

*Query the graph to find labels for nodes with the title property, _Forrest Gump_.*

[source, cypher]
----
MATCH (m)
WHERE m.title = 'Forrest Gump'
RETURN  labels(m)
----

The result returned should be:

[.thumb]
image::AllForrestGumps.png[AllForrestGumps,width=600]

== Exercise 9.5: Use `MERGE` to update a _Production_ node (Instructions)

*Use `MERGE` to update (`ON MATCH`) the existing _Production_ node for _Forrest Gump_ to add the _company_ property with a value of _Paramount Pictures_.*

== Exercise 9.5: Use `MERGE` to update a _Production_ node (Solution)

*Use `MERGE` to update (`ON MATCH`) the existing _Production_ node for _Forrest Gump_ to add the _company_ property with a value of _Paramount Pictures_.*

[source, cypher]
----
MERGE (p:Production {title: 'Forrest Gump'})
ON MATCH SET p.company = 'Paramount Pictures'
RETURN p
----

The result returned should be:

[.thumb]
image::AddCompany.png[AddCompany,width=700]

== Exercise 9.6: Use `MERGE` to add a label to a node (Instructions)

*Use `MERGE` to add the _OlderMovie_ label to the movie, _Forrest Gump_.*

== Exercise 9.6: Use `MERGE` to add a label to a node (Solution)

*Use `MERGE` to add the _OlderMovie_ label to the movie, _Forrest Gump_.*

[source, cypher]
----
MERGE (m:Movie {title: 'Forrest Gump'})
ON MATCH SET m:OlderMovie
RETURN labels(m)
----

The result returned should be:

[.thumb]
image::AddOlderMovieLabelToForrestGump.png[AddOlderMovieLabelToForrestGump,width=700]

== Exercise 9.7: Use `MERGE` to create two nodes and a single relationship (Instructions)

In this Part of the exercise, you merge data to create relationships.

*Execute the following Cypher statement that uses `MERGE` to create two nodes and a single relationship*

[source, cypher]
----
MERGE (p:Person {name: 'Robert Zemeckis'})-[:DIRECTED]->(m {title: 'Forrest Gump'})
----

This statement first finds all _Person_ nodes that have only the _name_ property value of _Robert Zemeckis_.
It then finds all nodes with only the _title_ property set to _Forrest Gump_.
There are no _Person_ or other nodes that have only these properties so the graph engine creates them.
Then the graph engine creates the relationship between these two nodes.
That is, this `MERGE` operation creates two nodes and a single relationship.
If we had provided all of the property values for the nodes, we would not have created the extra nodes.

In fact, you should *never* create nodes and relationships together like this! This example is here to show you how powerful Cypher can be.
A best practice is to create nodes first, then relationships.

== Exercise 9.7: Use `MERGE` to create two nodes and a single relationship (Solution)

The result returned should be:

[.thumb]
image::MergeNodesAndRelationships.png[MergeNodesAndRelationships,width=400]

== Exercise 9.8: Use the same `MERGE` statement to attempt to create two nodes and a single relationship (Instructions)

*Repeat the execution of the previous statement.*

It should do nothing.
A best practice is to always use `MERGE` to create relationships to ensure that there will be no duplication in the graph.

[source, cypher]
----
MERGE (p:Person {name: 'Robert Zemeckis'})-[:DIRECTED]->(m {title: 'Forrest Gump'})
----

== Exercise 9.8: Use the same `MERGE` statement to attempt to create two nodes and a single relationship (Solution)

*Repeat the execution of the previous statement.*

The result returned should be:

[.thumb]
image::MergeNodesAndRelationships2.png[MergeNodesAndRelationships2,width=400]

== Exercise 9.9: Find the correct _Person_ node to delete (Instructions)

*Find the correct _Person_ node to delete*

You query the nodes before you delete them to ensure you have the correct `MATCH` clauses.

Execute this query:

[source, cypher]
----
MATCH (p:Person {name: 'Robert Zemeckis'})-[rel]-(x)
WHERE NOT EXISTS (p.born)
RETURN p, rel, x
----

== Exercise 9.9: Find the correct _Person_ node to delete (Solution)

*Find the correct _Person_ node to delete*

The result returned should be:

[.thumb]
image::FindPersonNodeBeforeDelete.png[FindPersonNodeBeforeDelete,width=400]

== Exercise 9.10: Delete this _Person_ node, along with its relationships (Instructions)

*Delete this _Person_ node, along with its relationships.*

== Exercise 9.10: Delete this _Person_ node, along with its relationships (Solution)

*Delete this _Person_ node, along with its relationships.*

[source, cypher]
----
MATCH (p:Person {name: 'Robert Zemeckis'})--()
WHERE NOT EXISTS (p.born)
DETACH DELETE p
----

The result returned should be:

[.thumb]
image::DeletedExtraPerson.png[DeletedExtraPerson,width=400]

== Exercise 9.11: Find the correct _Forrest Gump_ node to delete (Instructions)

*Find the correct _Forrest Gump_ node to delete by executing this statement:*

[source, cypher]
----
MATCH (m)
WHERE m.title = 'Forrest Gump' AND labels(m) = []
RETURN m, labels(m)
----

== Exercise 9.11: Find the correct _Forrest Gump_ node to delete (Solution)

The result returned should be:

[.thumb]
image::ForrestGumpToDelete.png[ForrestGumpToDelete,width=400]

== Exercise 9.12: Delete the _Forrest Gump_ node (Instructions)

*Delete this _Forrest Gump_ node.*

It should have no relationships, but you can specify DETACH just to be certain.

== Exercise 9.12: Delete the _Forrest Gump_ node (Solution)

*Delete this _Forrest Gump_ node.*

[source, cypher]
----
MATCH (m)
WHERE m.title = 'Forrest Gump' AND labels(m) = []
DETACH DELETE m
----

The result returned should be:

[.thumb]
image::DeletedExtraForrestGump.png[DeletedExtraForrestGump,width=400]

== Exercise 9.13: Use `MERGE` to create the _DIRECTED_ relationship (Instructions)

*Use `MERGE` to create the _DIRECTED_ relationship between _Robert Zemeckis_ and  the _Movie_, _Forrest Gump_.*

== Exercise 9.13: Use `MERGE` to create the _DIRECTED_ relationship (Solution)

*Use `MERGE` to create the _DIRECTED_ relationship between _Robert Zemeckis_ and  the _Movie_, _Forrest Gump_.*

[source, cypher]
----
MATCH (p:Person), (m:Movie)
WHERE p.name = 'Robert Zemeckis' AND m.title = 'Forrest Gump'
MERGE (p)-[:DIRECTED]->(m)
----

The result returned should be:

[.thumb]
image::AddDirectedToForrestGump.png[AddDirectedToForrestGump,width=400]

== Exercise 9.14: Use `MERGE` to create the _ACTED_IN_ relationship (Instructions)

*Use `MERGE` to create the _ACTED_IN_ relationship between the actors, _Tom Hanks_, _Gary Sinise_, and _Robin Wright_ and the _Movie_, _Forrest Gump_.*

== Exercise 9.14: Use `MERGE` to create the _ACTED_IN_ relationship (Solution)

*Use `MERGE` to create the _ACTED_IN_ relationship between the actors, _Tom Hanks_, _Gary Sinise_, and _Robin Wright_ and the _Movie_, _Forrest Gump_.*

[source, cypher]
----
MATCH (p:Person), (m:Movie)
WHERE p.name IN ['Tom Hanks','Gary Sinise', 'Robin Wright']
      AND m.title = 'Forrest Gump'
MERGE (p)-[:ACTED_IN]->(m)
----

The result returned should be:

[.thumb]
image::ActedInForrestGump.png[ActedInForrestGump,width=400]

== Exercise 9.15: Modify the _role_ relationship property (Instructions)

*Modify the relationship property, _role_ for their roles in _Forrest Gump_:*

* *Tom Hanks is Forrest Gump*
* *Gary Sinise is Lt. Dan Taylor*
* *Robin Wright is Jenny Curran*

== Exercise 9.15: Modify the _role_ relationship property (Solution)

*Modify the relationship property, _role_ for their roles in _Forrest Gump_:*

* *Tom Hanks is Forrest Gump*
* *Gary Sinise is Lt. Dan Taylor*
* *Robin Wright is Jenny Curran*

[source, cypher]
----
MATCH (p:Person)-[rel:ACTED_IN]->(m:Movie)
WHERE m.title = 'Forrest Gump'
SET rel.roles = 
CASE p.name
  WHEN 'Tom Hanks' THEN ['Forrest Gump']
  WHEN 'Robin Wright' THEN ['Jenny Curran']
  WHEN 'Gary Sinise' THEN ['Lt. Dan Taylor']
END
----

The result returned should be:

[.thumb]
image::SetActedRoles.png[SetActedRoles,width=400]

== Exercise 9: Merging Data in the Graph (Summary)

In this exercise you added and modified nodes and relationships in the graph by merging data.
In some parts of the exercise, you created duplicate nodes and relationships.
You should take care not to create duplicate nodes and relationships in your real application graphs.

Congratulations! You have completed the exercises for this course.
