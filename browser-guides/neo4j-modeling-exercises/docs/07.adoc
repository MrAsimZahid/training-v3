= Exercise 7
:icons: font

== Exercise 7: Creating specific relationships  (Preparations)

Before you begin this exercise, make sure that you have performed the refactoring from Exercise 6.

This is what you should see when you click the database icon image:database-icon.png[].

image::AfterExercise6.png[AfterExercise6,width=300,role=left]

*If your database does not have the same nodes and relationships, you can execute these Cypher statements to reset your graph to what it should be before you start this exercise.*

[source,Cypher]
----
include::scripts/AfterExercise_6.cypher[]
----

*Note*: You must use the "file:///" syntax for loading the data from the import folder.

== Exercise 7: Creating specific relationships (Overview)

Neo4j is optimized for searching by unique relationship types and in this case the date of a flight provides that uniqueness.

You will refactor the model to change the `HAS_DAY` relationship to be the date of the flight instead, for example `ON_2019-1-3`.

Here are the tasks you will perform:

* *Exercise 7.1*: Refactor the graph to change all `HAS_DAY` relationships to specific day relationships.
* *Exercise 7.2*: Rewrite and profile the previous query that finds all flights from an airport to another on a given day.

Go to the next page to start this exercise.

== Exercise 7.1:  Refactor the graph to change all `HAS_DAY` relationships to specific day relationships. (Instructions)

Rather than having the `HAS_DAY` relationship from `Airport` nodes to `AirportDay` nodes, we want a specific relationship that will be `ON_yyyy-mm-dd`.
The revised model is:

image::Ex7_Model.png[Ex7_Model,800]

*Write and execute the Cypher statement to refactor the model.*

*Hint*: Use apoc.create.relationship().

== Exercise 7.1:  Refactor the graph to change all `HAS_DAY` relationships to specific day relationships. (Solution)

*Write and execute the Cypher statement to refactor the model.*

*Hint*: Use apoc.create.relationship().

[source,Cypher]
----
MATCH (origin:Airport)-[hasDay:HAS_DAY]->(ad:AirportDay)
CALL apoc.create.relationship(startNode(hasDay),
                              'ON_' + ad.date,
                              {},
                              endNode(hasDay) ) YIELD rel
RETURN COUNT(*)
----

The result returned should be:

[.thumb]
image::Ex7_AddSpecificRelationships.png[Ex7_AddSpecificRelationships,width=600]

== Exercise 7.2:  Rewrite and profile the previous query that finds all flights from an airport to another on a given day. (Instructions)

Previously, we used this query to look for all flights that go from Las Vegas (LAS) to Chicago (MDW) on January 3, 2019 (2019-1-3).

[source,Cypher]
----
PROFILE MATCH (origin:Airport {code: 'LAS'})-[:HAS_DAY]->(:AirportDay
      {date: '2019-1-3'})<-[:ORIGINATES_FROM]-(flight:Flight),
      (flight)-[:LANDS_IN]->(:AirportDay
      {date: '2019-1-3'})<-[:HAS_DAY]-(destination:Airport {code: 'MDW'})
RETURN origin, destination, flight
----

*Modify this query to use the refactored graph and execute the query.*

== Exercise 7.2:  Rewrite and profile the previous query that finds all flights from airport to another on a given day. (Solution)

*Modify this query to use the refactored graph and execute the query.*

[source,Cypher]
----
PROFILE
MATCH (origin:Airport {code: 'LAS'})-
[:`ON_2019-1-3`]->(originDay:AirportDay),
(originDay)<-[:ORIGINATES_FROM]-(flight:Flight),
(flight)-[:LANDS_IN]->(destinationDay),
(destinationDay:AirportDay)<-[:`ON_2019-1-3`]-
(destination:Airport {code: 'MDW'})
RETURN flight.date, flight.number, flight.airline,
flight.departure, flight.arrival
ORDER BY flight.date, flight.departure
----

The result returned should be:

[.thumb]
image::Ex7_FindFlightsJan3.png[Ex7_FindFlightsJan3,width=300]

The previous query before this refactoring yielded 1813 db hits.
After this refactoring and rewriting the query, we see 1716 db hits, a slight improvement.
If the graph had more data, we would probably see a difference in the performance after the last refactoring.

== Exercise 7: Creating specific relationships  (Taking it further)

Execute these previous two queries to see if the refactoring to specific relationships changed their performance:

Previously, this query showed 100 db hits:

[source,Cypher]
----
PROFILE
MATCH (origin)<-[:ORIGINATES_FROM]-(flight:Flight)-
      [:LANDS_IN]->(destination)
WHERE flight.airline = 'WN' AND
      flight.number = '1016' RETURN origin, destination, flight
----

Previously, this query showed 1408 db hits:

[source,Cypher]
----
PROFILE
MATCH (f:Flight)
WHERE f.flightId ENDS WITH 'LAS'
RETURN f.flightId
----

== Exercise 7: Creating specific relationships  (Summary)

In this exercise, you refactored the `HAS_DAY` relationship between the `Airport` and the `AirportDay` to be a specific date relationship.
For the existing graph with 10K flight nodes, this change did not make a significant difference.
If the graph had more data, we would probably see an improvement.

In the next exercise, you will load even more data in to the graph.

ifdef::env-guide[]
pass:a[<a play-topic='{guides}/08.html'>Continue to Exercise 8</a>]
endif::[]
