= Exercise 17

== Exercise 17: Using APOC for importing data (Preparations)

You will start with a new database named importwithapoc. Execute these commands in Neo4j Browser to create the database.

[source, cypher]
----
:USE system
----

[source, cypher]
----
CREATE DATABASE importwithapoc
----
[source, cypher]
----
:USE importwithapoc
----

== Exercise 17: Using APOC for importing data (Overview)

In this exercise you execute Cypher statements to import data into your graph using the APOC library.

* *Exercise 17.1*: Install APOC into your project in Neo4j Desktop (if you are using Neo4j Desktop).
* *Exercise 17.2*: Verify that you can access APOC procedures.
* *Exercise 17.3*: Write Cypher code to examine the data.
* *Exercise 17.4*: Create constraints.
* *Exercise 17.5*: Load the data with APOC.
* *Exercise 17.6*: Write and execute Cypher and APOC code to clear everything from the graph.

Go to the next page to start this exercise.

== Exercise 17.1: Install APOC into your project in Neo4j Desktop (Instructions/Solution)

*Note:* If you are using a Neo4j Sandbox or Neo4j Aura, you already have APOC installed and you can skip this step.

. Stop the Neo4j instance.
. For the project you are working with, select the APOC plugin to install.
. Start the Neo4j instance.
. In Neo4j Browser ensure that you have selected the importwithapoc database

== Exercise 17.2: Verify that you can access APOC procecdures (Instructions)

*Execute the Cypher code to list the APOC procedures.*

== Exercise 17.2: Verify that you can access APOC procecdures (Solution)

*Execute the Cypher code to list the APOC procedures.*

[source, cypher]
----
CALL dbms.procedures()
YIELD name WHERE name STARTS WITH "apoc"
RETURN name
----

The result will be:

[.thumb]
image::APOCinstalled.png[APOCinstalled,width=500]


== Exercise 17.3: Write code to inspect the data (Instructions)

The data for the load comes from this file where we have denormalized data:

https://data.neo4j.com/v4.0-intro-neo4j/movies2.csv

*Note:* If you have previously downloaded the files for this course and are using Neo4j Desktop, you can place movies2.csv into the *import* folder:

. In Neo4j Desktop, click the 3 dots to the right of the DBMS.
. Click *Open Folder > Import*.
. Copy the CSV files you previously downloaded to the import folder.
. Add this property to the neo4j.conf file: apoc.import.file.enabled=true
. You will adapt the code to read from the filesystem: file:///movies2.csv'

*Note:* If you are using Neo4j Sandbox or Neo4j Aura, you must use the URL for the file at *data.neo4j.com*.

*Write and execute Cypher code to view the first 100 rows of this file.*

== Exercise 17.3: Write code to inspect the data (Solution)

The data for the load comes from this file where we have denormalized data:

https://data.neo4j.com/v4.0-intro-neo4j/movies2.csv

*Write and execute Cypher code to view the first 100 rows of this file.*

[source, cypher]
----
LOAD CSV WITH HEADERS FROM
     'https://data.neo4j.com/v4.0-intro-neo4j/movies2.csv' AS row
RETURN row LIMIT 100
----

The result will be:

[.thumb]
image::ViewAPOCdataToLoad.png[ViewAPOCdataToLoad,width=700]


== Exercise 17.4: Create constraints (Instructions)

*Create uniqueness constraints for the _id_ property for a _Person_ node and the _id_ property for a _Movie_ node.*

== Exercise 17.4: Create constraints (Solution)

*Create uniqueness constraints for the _id_ property for a _Person_ node and the _id_ property for a _Movie_ node.*

[source, cypher]
----
// create constraints
CREATE CONSTRAINT UniqueMovieIdConstraint ON (m:Movie) ASSERT m.id IS UNIQUE;

CREATE CONSTRAINT UniquePersonIdConstraint ON (p:Person) ASSERT p.id IS UNIQUE
----

The result will be:

[.thumb]
image::APOCLoadConstraints.png[APOCLoadConstraints,width=500]

== Exercise 17.5: Load the data with APOC (Instructions)

*Since APOC is new to you, examine this code and simply execute this code to load the data:*

*Note*: Depending on your network speed, this code will take up to 10 minutes to execute.

[source, cypher]
----
CALL apoc.periodic.iterate(
"CALL apoc.load.csv('https://data.neo4j.com/v4.0-intro-neo4j/movies2.csv' ) YIELD map AS row RETURN row",
"WITH row.movieId as movieId, row.title AS title, row.genres AS genres, toInteger(row.releaseYear) AS releaseYear, toFloat(row.avgVote) AS avgVote,
 collect({id: row.personId, name:row.name, born: toInteger(row.birthYear), died: toInteger(row.deathYear),personType: row.personType, roles: split(coalesce(row.characters,''),':')}) AS people
 MERGE (m:Movie {id:movieId})
    ON CREATE SET m.title=title, m.avgVote=avgVote,
       m.releaseYear=releaseYear, m.genres=split(genres,':')
 WITH *
 UNWIND people AS person
 MERGE (p:Person {id: person.id})
    ON CREATE SET p.name = person.name, p.born = person.born, p.died = person.died
 WITH  m, person, p
 CALL apoc.do.when(person.personType = 'ACTOR',
      'MERGE (p)-[:ACTED_IN {roles: person.roles}]->(m)
                 ON CREATE SET p:Actor',
      'MERGE (p)-[:DIRECTED]->(m)
          ON CREATE SET p:Director',
      {m:m, p:p, person:person}) YIELD value AS value
       RETURN count(*)  ",
{batchSize: 500}
)
----

== Exercise 17.5: Load the data with APOC (Solution)

The result will be:

[.thumb]
image::APOCExecute.png[APOCExecute,width=800]

== Exercise 17.6: Write and execute Cypher and APOC code to clear everything from the graph (Instructions)

*Call the APOC procedures to clear everything from the graph, including constraints and indexes.*

== Exercise 17.6: Write and execute Cypher and APOC code to clear everything from the graph (Solution)

*Call the APOC procedures to clear everything from the graph, including constraints and indexes.*

Here is the code you must execute:

[source, cypher]
----
// Delete all constraints and indexes
CALL apoc.schema.assert({},{},true);
// Delete all nodes and relationships
CALL apoc.periodic.iterate(
  'MATCH (n) RETURN n',
  'DETACH DELETE n',
  { batchSize:500 }
)
----

The result returned will be:

[.thumb]
image::ClearedDB.png[ClearedDB,width=800]

Note: This is the same as doing the following in Neo4j Browser  where, for example, the name of the database is _neo4j_:

[source, cypher]
----
:USE system
CREATE OR REPLACE DATABASE neo4j
:USE neo4j
----

== Exercise 17: Using APOC for importing data (Summary)

In this exercise you executed Cypher statements to import data into your graph using the APOC library.

ifdef::env-guide[]
pass:a[<a play-topic='{guides}/18.html'>Continue to Exercise 18</a>]
endif::[]
