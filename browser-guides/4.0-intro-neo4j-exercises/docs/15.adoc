= Exercise 15
:imagedir: ../images

== Exercise 15: Using query best practices (Preparations)


Make sure you have the Movie database, which contains 176 nodes and 257 relationships.

This is what you will see when you click the database icon:

image::AfterExercise13.png[AfterExercise13,width=300]

If your database does not have this number of nodes and relationships, you must do the following in Neo4j Browser to recreate the database (assuming you are using the _neo4j_ database):

[source, cypher]
----
:USE system
CREATE OR REPLACE DATABASE neo4j
:USE neo4j
----

Then,  use the script below to populate it, including the constraints added in a previous exercise:

[source, cypher]
----
include::scripts/AfterExercise13.cypher[]
----

== Exercise 15: Using Query best practices (Overview)

You have gained experience with the basics of accessing a Neo4j graph by querying it, creating nodes and relationships, and updating the graph.

In this exercise you enhance Cypher statements you write so that you can parameterize values used during the execution of your Cypher statements.
Then you gain experience with viewing query plans, monitoring queries, and killing queries.

* *Exercise 15.1*: Execute a Cypher query as described.
* *Exercise 15.2*: Add a parameter to your session.
* *Exercise 15.3*: Modify the Cypher query you just wrote to use a parameter.
* *Exercise 15.4*: Modify parameter value and retest your query.
* *Exercise 15.5*: Add a different parameter to your session.
* *Exercise 15.6*: Modify the query you wrote previously to use the second parameter.
* *Exercise 15.7*: Modify the second parameter value and retest your query.
* *Exercise 15.8*: View the query plan for a Cypher statement.
* *Exercise 15.9*: View the metrics for the query when a statement executes.
* *Exercise 15.10*: Remove the labels from the nodes and relationships in the query and again view the metrics.
* *Exercise 15.11*: Open a second Neo4j Browser session. 
* *Exercise 15.12*: Execute a query that returns a lot of results and monitor.
* *Exercise 15.13*: Execute a long-running query and monitor.
* *Exercise 15.14*: Kill a query.

Go to the next page to start this exercise.

== Exercise 15.1: Execute a Cypher query as described (Instructions)

Suppose that you want to create Cypher statements that enable you to easily test against a number of values in the graph.
You will be exploring the graph for people who reviewed movies and the actors in these movies.
You do not want to hard-code the value for _released_ for a _Movie_ node in your query.

*Write and execute a Cypher query that returns the names of people who reviewed movies and the actors in these movies by returning the name of the reviewer, the movie title reviewed, the release date of the movie, the rating given to the movie by the reviewer, and the list of actors for that particular movie.*

== Exercise 15.1: Execute a Cypher query as described (Solution)

*Write and execute a Cypher query that returns the names of people who reviewed movies and the actors in these movies by returning the name of the reviewer, the movie title reviewed, the release date of the movie, the rating given to the movie by the reviewer, and the list of actors for that particular movie.*

[source, cypher]
----
MATCH (r:Person)-[rel:REVIEWED]->(m:Movie)<-[:ACTED_IN]-(a:Person)
RETURN  DISTINCT r.name, m.title, m.released, rel.rating, collect(a.name)
----

The result returned will be:

[.thumb]
image::InitialQueryWithoutParms.png[InitialQueryWithoutParms,width=1000]

== Exercise 15.2: Add a parameter to your session (Instructions)

*Add a parameter named _year_ to your session with a value of _2000_.*

== Exercise 15.2: Add a parameter to your session (Solution)

*Add a parameter named _year_ to your session with a value of _2000_.*

[source, cypher]
----
:param year => 2000
----

The result returned will be:

[.thumb]
image::SetYearParam.png[SetYearParam,width=800]

== Exercise 15.3: Modify the Cypher query you just wrote to use a parameter (Instructions)

*Modify the Cypher query you just wrote to filter by the _year_ parameter.*

== Exercise 15.3: Modify the Cypher query you just wrote to use a parameter (Solution)

*Modify the Cypher query you just wrote to filter by the _year_ parameter.*

[source, cypher]
----
MATCH (r:Person)-[rel:REVIEWED]->(m:Movie)<-[:ACTED_IN]-(a:Person)
WHERE m.released = $year
RETURN  DISTINCT r.name, m.title, m.released, rel.rating, collect(a.name)
----

The result returned will be:

[.thumb]
image::UsingOneParameter.png[UsingOneParameter,width=800]

== Exercise 15.4: Modify parameter value and retest your query (Instructions)

. *Modify the _year_ parameter to be a different value, _2006_.*
. *Retest your query.*

== Exercise 15.4: Modify parameter value and retest your query (Solution)


. *Modify the _year_ parameter to be a different value, _2006_.*
+

[source, cypher]
----
:param year => 2006
----

[.thumb]
image::UsingOneParameter2.png[UsingOneParameter2,width=800]

. *Retest your query.*

[source, cypher]
----
MATCH (r:Person)-[rel:REVIEWED]->(m:Movie)<-[:ACTED_IN]-(a:Person)
WHERE m.released = $year
RETURN  DISTINCT r.name, m.title, m.released, rel.rating, collect(a.name)
----

The result returned  with the same query will be:

[.thumb]
image::UsingOneParameter3.png[UsingOneParameter3,width=600]

== Exercise 15.5: Add a different parameter to your session (Instructions)

Suppose that you want to parameterize both the values in your query for _released_ for a _Movie_ node and the _rating_ value for the _REVIEWED_ relationship.

*Add a parameter named _ratingValue_ to your session with a value of _65_.*

== Exercise 15.5: Add a different parameter to your session (Solution)

*Add a parameter named _ratingValue_ to your session with a value of _65_.*

[source, cypher]
----
:params {year: 2006, ratingValue: 65}
----

The result returned will be:

[.thumb]
image::SetTwoParameters.png[SetTwoParameters,width=800]

== Exercise 15.6: Modify the query you wrote previously to use the second parameter (Instructions)

*Modify the query you wrote previously to also filter the result returned by  the _rating_ for the movie.* 

== Exercise 15.6: Modify the query you wrote previously to use the second parameter (Solution)

*Modify the query you wrote previously to also filter the result returned by  the _rating_ for the movie.* 

[source, cypher]
----
MATCH (r:Person)-[rel:REVIEWED]->(m:Movie)<-[:ACTED_IN]-(a:Person)
WHERE m.released = $year AND
      rel.rating > $ratingValue
RETURN  DISTINCT r.name, m.title, m.released, rel.rating, collect(a.name)
----

For the above statement, The result returned will be:

[.thumb]
image::UseTwoParameters.png[UseTwoParameters,width=800]

== Exercise 15.7: Modify the second parameter value and retest your query (Instructions)

*Modify the _ratingValue_ parameter to be a different value, _60_, and retest your query.*

== Exercise 15.7: Modify the second parameter value and retest your query (Solution)

*Modify the _ratingValue_ parameter to be a different value, _60_, and retest your query.*

[source, cypher]
----
:params {year: 2006, ratingValue: 60}
----

The result returned with the new parameter value will be:

[.thumb]
image::UseTwoParameters2.png[UseTwoParameters2,width=800]

== Exercise 15.8: View the query plan for a Cypher statement (Instructions)

For this Part of the exercise, you will use the query that you wrote previously using Cypher parameters.
It assumes that you have set the _year_ and _ratingValue_ Cypher parameters:

[source, cypher]
----
MATCH (r:Person)-[rel:REVIEWED]->(m:Movie)<-[:ACTED_IN]-(a:Person)
WHERE m.released = $year AND
      rel.rating > $ratingValue
RETURN  DISTINCT r.name, m.title, m.released, rel.rating, collect(a.name)
----

*View the query plan for this Cypher statement.*

== Exercise 15.8: View the query plan for a Cypher statement (Solution)

*View the query plan for this Cypher statement.*

[source, cypher]
----
EXPLAIN MATCH (r:Person)-[rel:REVIEWED]->(m:Movie)<-[:ACTED_IN]-(a:Person)
WHERE m.released = $year AND
      rel.rating > $ratingValue
RETURN  DISTINCT r.name, m.title, m.released, rel.rating, collect(a.name)
----

When The result returned will be:

[.thumb]
image::EXPLAIN.png[EXPLAIN,width=250]

== Exercise 15.9: View the metrics for the query when a statement executes (Instructions)

*View the metrics for the query when the previous statement executes.*

== Exercise 15.9: View the metrics for the query when a statement executes (Solution)

*View the metrics for the query when the previous statement executes.*

[source, cypher]
----
PROFILE MATCH (r:Person)-[rel:REVIEWED]->(m:Movie)<-[:ACTED_IN]-(a:Person)
WHERE m.released = $year AND
      rel.rating > $ratingValue
RETURN  DISTINCT r.name, m.title, m.released, rel.rating, collect(a.name)
----

The result returned will look something like this:

[.thumb]
image::PROFILE.png[PROFILE,width=250]

== Exercise 15.10: Remove the labels from the nodes and relationships in the query and again view the metrics (Instructions)

*Remove the labels from the nodes and relationships in the query and again view the metrics. Compare the db hits from the previous version of the statement.*

== Exercise 15.10: Remove the labels from the nodes and relationships in the query and again view the metrics (Solution)

*Remove the labels from the nodes and relationships in the query and again view the metrics. Compare the db hits from the previous version of the statement.*

[source, cypher]
----
PROFILE MATCH (r)-[rel]->(m)<-[:ACTED_IN]-(a)
WHERE m.released = $year AND
      rel.rating > $ratingValue
RETURN  DISTINCT r.name, m.title, m.released, rel.rating, collect(a.name)
----

The result returned will be:

[.thumb]
image::PROFILE2.png[PROFILE2,width=700]

You will see more db hits.
This is because you removed the labels that help to filter nodes and relationships during the query.

== Exercise 15.10: Remove the labels from the nodes and relationships in the query and again view the metrics (Taking it further - optional)

* Explore the contents of each step of the query by opening each step.
* Perform other queries to view the steps of the queries.

== Exercise 15.11: Open a second Neo4j Browser session (Instructions)

Recall that a query may run for a long time because:

* There are a lot of results to return
* The query takes a long time to execute in the graph engine.

You will perform these steps to gain some experience with monitoring and killing queries by having your original Neo4j Browser window open and then opening another Neo4j Browser window.

*Open a second Neo4j Browser session in a Web browser. This session will be used for monitoring queries as they run.*

== Exercise 15.11: Open a second Neo4j Browser session (Solution)

*Open a second Neo4j Browser session in a Web browser. This session will be used for monitoring queries as they run.*

If you are using Neo4j Desktop, open a Web browser window and enter http://localhost:7474 which opens a second Neo4j Browser window that has access to the same graph.

[.thumb]
image::SecondBrowserWindowDesktop.png[SecondBrowserWindowDesktop,width=500]

*Note:* If you are using a Neo4j Sandbox or Neo4j Aura, go the the Sandbox or Aura site and simply click on the Neo4j Browser to open a second window for that same graph.

== Exercise 15.12: Execute a query that returns a lot of results and monitor (Instructions)

*Execute this code in your original Neo4j Browser session that returns a lot of results.
In the second Neo4j Browser window, monitor the running queries.*

Here is a very *bad* Cypher statement to use that returns a lot of results:

[source, cypher]
----
PROFILE MATCH (a)--(b)--(c)--(d)--(e)--(f)--(g)
RETURN a
----

What do you see in the second Neo4j Browser window where you are monitoring queries with the *:queries* command?

== Exercise 15.12: Execute a query that returns a lot of results and monitor (Solution)

*Execute this code in your original Neo4j Browser session that returns a lot of results.
In the second Neo4j Browser window, monitor the running queries.*

Here is a very *bad* Cypher statement to use that returns a lot of results:

[source, cypher]
----
PROFILE MATCH (a)--(b)--(c)--(d)--(e)--(f)--(g)
RETURN a
----

What do you see in the second Neo4j Browser window where you are monitoring queries with the *:queries* command?

When you first execute the query in the original Neo4j Browser window, you will see this in the second Neo4j Browser window:

[.thumb]
image::LongRunningReturn1.png[LongRunningReturn1,width=900]

Initially, the query is running in the server.
After a few moments, you will see that in the original Neo4j Browser window, the query appears to be running, but it has completed on the server:

[.thumb]
image::LongRunningReturn2.png[LongRunningReturn2,width=900]

This is because the graph engine has completed the query and the results are still being streamed to the client.

== Exercise 15.13: Execute a long-running query and monitor (Instructions)

*Execute this long-running query in your original Neo4j Browser session and monitor the query in the second Neo4j Browser session.*

Here is a very *bad* Cypher statement that takes a lot of time to execute in the graph engine:

[source, cypher]
----
PROFILE MATCH (a), (b), (c), (d), (e) , (f), (g)
RETURN count(id(a))
----

What do you see in the second Neo4j Browser window where you are monitoring queries with the *:queries* command?

== Exercise 15.13: Execute a long-running query and monitor (Solution)

*Execute this long-running query in your original Neo4j Browser session and monitor the query in the second Neo4j Browser session.*

Here is a very *bad* Cypher statement that takes a lot of time to execute in the graph engine:

[source, cypher]
----
PROFILE MATCH (a), (b), (c), (d), (e) , (f), (g)
RETURN count(id(a))
----

What do you see in the second Neo4j Browser window where you are monitoring queries with the *:queries* command?

You will see this when you first start the query:

[.thumb]
image::LongQuery1.png[LongQuery1,width=900]

It takes a VERY long time to complete on the server.

After a while you will want to kill this long-running query.

== Exercise 15.14: Kill a query (Instructions)

*In the second Neo4j Browser window, kill the long-running query.*

What do you see in the original Neo4j Browser window?

== Exercise 15.14: Kill a query (Solutions)

*In the second Neo4j Browser window, kill the long-running query.*

Here is where you select in the UI that you want to kill the running query:

[.thumb]
image::LongQuery2.png[LongQuery2,width=900]

You will now see this in the original Neo4j Browser window:

[.thumb]
image::LongQuery3.png[LongQuery3,width=600]

== Exercise 15: Using query best practices (Summary)

In this exercise you enhanced Cypher statements you write so that you can parameterize values used during the execution of your Cypher statements.
Then you gained experience with viewing query plans, monitoring queries, and killing queries.

ifdef::env-guide[]
pass:a[<a play-topic='{guides}/16.html'>Continue to Exercise 16</a>]
endif::[]
